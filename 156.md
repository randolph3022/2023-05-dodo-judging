Jiamin

high

# User's Debt Positions could be manipulated by Attacker

## Summary
Attacker or malicious user can manipulate other user's debt positions without being allowed proxy.

## Vulnerability Detail
[MarginTrading](https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L19) contract serves as a user-managed asset contract, responsible for interacting with Aave, including functions such as opening, closing, repaying, and withdrawing.
This contract provides [executeFlashLoans(address[],uint256[],uint256[],address,bytes)](https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L89) method, which is used for opening and closing on AAVE.
```solidity
    function executeFlashLoans(
        address[] calldata assets,
        uint256[] calldata amounts,
        uint256[] calldata modes,
        address mainToken,
        bytes calldata params
    ) external onlyFlashLoan {
        address receiverAddress = address(this);

        // the various assets to be flashed

        // the amount to be flashed for each asset

        // 0 = no debt, 1 = stable, 2 = variable

        address onBehalfOf = address(this);
        // bytes memory params = "";
        lendingPool.flashLoan(receiverAddress, assets, amounts, modes, onBehalfOf, params, Types.REFERRAL_CODE);
        emit FlashLoans(assets, amounts, modes, mainToken);
    }
```
When this method is called, system will first check if caller is authorized through [__onlyFlashLoan__](https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#LL43C21-L43C21) modifier:
```solidity
    modifier onlyFlashLoan() {
        require(
            _USER == msg.sender || owner() == msg.sender
                || IMarginTradingFactory(owner()).isAllowedProxy(address(this), msg.sender),
            "caller is unauthorized"
        );
        _;
    }
```
By doing this, system can prevent malicious user from manipulating debt positions, however, attacker can bypass this check by calling flashloan() method directly on AAVE, as it is said in  this [document]((https://docs.aave.com/developers/v/2.0/guides/flash-loans)):
>After some sanity checks, the LendingPool transfers the requested amounts of the reserves to your contract, then calls executeOperation() on your contract (or another contract that you specify as the _receiver)

[__executeOperation(address[],uint256[],uint256[],address,bytes)__](https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L121) will then be called, please note system does not check if ___initiator__ is the MarginTrading contract itself, so this method will not revert even if the initiator is attacker, and open or close debt positions based on ___params__.
```solidity
    function executeOperation(
        address[] calldata _assets,
        uint256[] calldata _amounts,
        uint256[] calldata _premiums,
        address _initiator,
        bytes calldata _params
    ) external override onlyLendingPool returns (bool) {
        //decode params exe swap and deposit
        {
            (
                uint8 _flag,
                address _swapAddress,
                address _swapApproveTarget,
                address[] memory _swapApproveToken,
                bytes memory _swapParams,
                address[] memory _tradeAssets,
                address[] memory _withdrawAssets,
                uint256[] memory _withdrawAmounts,
                uint256[] memory _rateMode,
                address[] memory _debtTokens
            ) = abi.decode(
                _params,
                (uint8, address, address, address[], bytes, address[], address[], uint256[], uint256[], address[])
            );
            if (_flag == 0 || _flag == 2) {
                //close
                _closetrade(
                    _flag,
                    _swapAddress,
                    _swapApproveTarget,
                    _swapApproveToken,
                    _swapParams,
                    _tradeAssets,
                    _withdrawAssets,
                    _withdrawAmounts,
                    _rateMode,
                    _debtTokens
                );
            }
            if (_flag == 1) {
                //open
                _opentrade(_swapAddress, _swapApproveTarget, _swapApproveToken, _swapParams, _tradeAssets);
            }
        }
        return true;
    }
```

## Impact
MaginTrading's debt postions will be manipulated by malicious user.

## Code Snippet
https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L121-L166

## Tool used

Manual Review

## Recommendation
As said in [ERC-3156](https://eips.ethereum.org/EIPS/eip-3156), it is recommended to verify that the initiator belongs to a group of trusted addresses, please add an checking for initiator in [__executeOperation(address[],uint256[],uint256[],address,bytes)__](https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L121):
```solidity
    require(_initiator == address(this));
```
