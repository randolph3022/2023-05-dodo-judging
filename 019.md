rvierdiiev

high

# User can get more eth into MarginTrading contract using MarginTradingFactory.multicall function

## Summary
User can get more eth into MarginTrading contract using MarginTradingFactory.multicall function.
## Vulnerability Detail
`MarginTradingFactory.multicall` allows to call several function in the contract.
https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTradingFactory.sol#L74-L87
```solidity
    function multicall(bytes[] calldata data) public payable returns (bytes[] memory results) {
        results = new bytes[](data.length);
        for (uint256 i = 0; i < data.length; i++) {
            (bool success, bytes memory result) = address(this).delegatecall(data[i]);

            if (!success) {
                assembly {
                    revert(add(result, 32), mload(result))
                }
            }

            results[i] = result;
        }
    }
```
It calls function using `address(this).delegatecall(data[i])`.
The problem is that in such call `msg.value` will be same for all calls. It will be initial `msg.value` sent to the `multicall` function.

Also `MarginTradingFactory` has `depositMarginTradingETH` function which takes paid eth and then deposits it into weth and sends to the `MarginTrading` contract.
https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTradingFactory.sol#L203C14-L211
```solidity
    function depositMarginTradingETH(address _marginTradingAddress, bool _margin, uint8 _flag) public payable {
        require(IMarginTrading(_marginTradingAddress).user() == msg.sender, "factory:caller is not the user");
        WETH.deposit{value: msg.value}();
        WETH.transfer(_marginTradingAddress, msg.value);
        if (_margin) {
            IMarginTrading(_marginTradingAddress).lendingPoolDeposit(address(WETH), msg.value, _flag);
        }
        emit DepositMarginTradingETH(_marginTradingAddress, msg.value, _margin, _flag);
    }
```
As you can see, here `msg.value` is used as value that is paid by user.

So user can call multiple `depositMarginTradingETH` functions inside `multicall` function in order to get whole `MarginTradingFactory` eth balance transferred to the user's `MarginTrading` contract.
## Impact
User can steal MarginTradingFactory eth balance. 
## Code Snippet
Provided above
## Tool used

Manual Review

## Recommendation
Add another param `values` into `multicall` function and send appropriate amount along the each call.